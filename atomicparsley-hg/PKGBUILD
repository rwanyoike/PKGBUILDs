# Maintainer: Raymond Wanyoike <raymond dot wanyoike at gmail dot com>

_gitauthor=wez
_gitname=atomicparsley

pkgname="${_gitname}"-hg
pkgver=r110.3c31f225a823
pkgrel=1
pkgdesc="A command line program for reading, parsing and setting iTunes-style metadata in MPEG4 files"
arch=("any")
url="https://bitbucket.org/${_gitauthor}/${_gitname}"
license=("GPL")
makedepends=("mercurial")
provides=("${_gitname}")
conflicts=("${_gitname}")
source=("hg+https://bitbucket.org/${_gitauthor}/${_gitname}")
sha256sums=("SKIP")

pkgver() {
  cd "${_gitname}"

  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

build() {
  cd "${_gitname}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${_gitname}"

  make DESTDIR="${pkgdir}" install
}
